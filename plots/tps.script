#!/usr/bin/gnuplot
resrepx=system("echo $RESREPX")
resrepy=system("echo $RESREPY")
set term svg enhanced size resrepx,resrepy
set output "tps.svg"
set title "TPS"
set grid xtics mxtics ytics mytics
set xlabel "Time during test"
set ylabel "TPS"
# A yrange min of 0, to avoid interpolation line changing range
max_tps=system("echo $MAX_TPS")
min_time=system("echo $MIN_TIME")
max_time=system("echo $MAX_TIME")
weight_param=system("echo $TPS_WEIGHT")
weightv=weight_param+0
titlev=sprintf("Interpolated TPS (Weighted spline, weight %1.2f )", weightv)
set yrange [0:max_tps]
set xdata time
set timefmt "%s"
set format x "%M:%S"
set xrange [min_time:max_time]
set lmargin at screen 0.15;
plot "tpsdata.txt" using 1:2 with lines notitle linecolor rgb "orange", \
"tpsdata.txt" using 1:2:(weightv) smooth acsplines with lines title titlev linewidth 3 linecolor rgb "red"
